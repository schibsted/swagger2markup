buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.10.1'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.0.0"
        classpath "org.ajoberstar:gradle-git:1.3.2"
    }
}
description = 'swagger2markup Build'
version = '1.3.3-SCHIBSTED'
ext.releaseVersion = '1.3.1'
group = 'io.github.swagger2markup'

apply plugin: 'java'
apply plugin: 'eclipse'
apply from: 'gradle/publishing.gradle'
apply from: 'gradle/coverage.gradle'
apply from: 'gradle/documentation.gradle'

tasks.withType(JavaCompile) {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    options.deprecation = true
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked"
}

repositories {
    maven {
        url "https://oss.jfrog.org/artifactory/oss-snapshot-local"
    }
    jcenter()
    mavenCentral()
    //mavenLocal()
}

dependencies {
    compile 'io.github.swagger2markup:markup-document-builder:1.1.1'
    compile 'io.swagger:swagger-compat-spec-parser:1.0.25'
    compile 'org.apache.commons:commons-configuration2:2.1'
    compile 'commons-beanutils:commons-beanutils:1.9.2'
    compile 'org.apache.commons:commons-collections4:4.1'
    compile 'io.javaslang:javaslang:2.0.5'
    compile 'ch.netzwerg:paleo-core:0.10.2'
    testCompile 'junit:junit:4.11'
    testCompile 'org.asciidoctor:asciidoctorj:1.5.4'
    testCompile 'ch.qos.logback:logback-classic:1.1.2'
    testCompile 'org.assertj:assertj-core:3.5.2'
    testCompile 'io.github.robwin:assertj-diff:0.1.1'
}

test {
    if (System.properties['http.proxyHost']) {
        systemProperty 'http.proxyHost', System.properties['http.proxyHost']
        systemProperty 'http.proxyPort', System.properties['http.proxyPort']
        systemProperty 'http.nonProxyHosts', System.properties['http.nonProxyHosts']
    }
    systemProperty 'file.encoding', 'UTF-8'
}


task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}
